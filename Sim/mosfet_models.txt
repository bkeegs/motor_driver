
.SUBCKT P_60_l_var dd g s  PARAMS: Tj=27 a=2.32 dVth=0 dR=0 dgfs=0 Inn=3.5 Unn=10 Rmax=67m gmin=1 Rs=1m dC=0 heat=0

.PARAM  kbq=85.8u    Tref=298      T0=273 
.PARAM  nsub=5.4     N=1.5         Vth0= 2.1   dib=5m        auth = 1.75m
.PARAM  c=0.65       nmu=1.5       ndi= 1.5    UBr = 70
.PARAM  aubr=60m     UTnbr=0.05    lnIb=-8.3   beta=1.8      lnIsj = -19.5
.PARAM  Rd=365m      ta=30n        td=30n      Rdi=40m  
.PARAM  Cgs0=140p    Cds0=139p     Cox0=250p   U0=1.03       Vn=0.15

.PARAM  Vmin=1.6     Vmax=2.6      gfmin=0.5   dCmax=0.33

.PARAM  Vth={Vth0+(Vmax-Vth0)*limit(dVth,0,1)-(Vmin-Vth0)*limit(dVth,-1,0)}
.PARAM  p1={(Unn-Inn*Rs-Vth)*beta*a}
.PARAM  p2={(dib-c)*Inn*beta*a}
.PARAM  Rlim={(p1+2*p2*Rmax-SQRT(p1**2+4*p2))/(2*p2)}
.PARAM  dRd={Rd/a+if(dVth==0,limit(dR,0,1)*max(Rlim-Rd/a-Rs,0),0)}
.PARAM  bm={c/((1/gmin-Rs)**2*Inn*a*(T0/Tref)**nmu)}
.PARAM  bet={(beta+(beta-bm)*if(dR==0,if(dVth==0,limit(dgfs,-1,0),0),0))}

.PARAM  dC1={1+dCmax*limit(dC,0,1)}
.PARAM  dC2={1+1.5*dCmax*limit(dC,0,1)}

.PARAM  Cgs={Cgs0*dC1}
.PARAM  Cox={Cox0*dC2}
.PARAM  Cds={Cds0*dC1}

*MOS+Diode   
.FUNC I0(Uds,Uee,ph,ps)  {if(Uee>ps,(Uee-c*min(Uds,Uee/(2*c)))*min(Uds,Uee/(2*c)),ph*(ps-ph)/c*exp((Uee-ps)/ph))}
.FUNC Imos(Uds,Ugs,T,ph) {bet/((T/Tref)**n)*I0(Uds,Ugs-Vth+dib*Uds+auth*(T-Tref),ph,min(2*ph,ph+c*Uds))}
.FUNC Iges(Uds,Ugs,T)    {a*sgn(Uds)*(Imos(abs(Uds),Ugs,T,nsub*kbq*T)+exp(min(lnIb+(Uds-Ubr-aubr*(T-Tref))/UTnbr,7)))}
.FUNC Isjt(T)            {exp(min(lnIsj+(T/Tref-1)*1.12/(ndi*kbq*T),7))*T/Tref}
.FUNC Idiode(Usd,T,Iss)  {exp(min(log(Iss)+Usd/(ndi*kbq*T),7))-Iss}

E_Edg  d1 ox VALUE {-max(-V(d1,g),0)+2*Vn*SQRT(1+max(-V(d1,g),0)/Vn)}
C_Cdg  ox g {Cox*a+5p}
E_Eds  d1 edep VALUE {V(d1,s)+2*(SQRT(U0*max(U0-V(d1,s),0))-U0)}
C_Cds  edep s {Cds*a}
C_Cgs  g  s {Cgs*a+5p}                   

G_G1 s d1 VALUE={Iges(V(s,d1),V(s,g),T0+limit(V(Tj),-200,999))+LIMIT(I(V_sense3),-1k,1k)}
G_drain d1 d2 VALUE={V(d1,d2)/(dRd*((limit(V(Tj),-200,999)+T0)/Tref)**nmu)}
V_sense d2 dd 0
G_diode d3 s VALUE={a*Idiode(V(d3,s),T0+limit(V(Tj),-200,999),Isjt(T0+limit(V(Tj),-200,999)))}
R_diode d3 d4 {Rdi/a}
V_sense2 d2 d4 0

C_C001    a   0 {ta*td/(ta+td)}
R_R001    a   b 1
V_sense3  b   c 0
E_E001    c   0 VALUE {ta/td*I(V_sense2)}

R1 g s 2G
Rd01 d1 s   500Meg
Rd02 d1 d2  1k

G_TH 0 Tj VALUE = {heat*LIMIT(I(V_sense)*V(s,dd),0,100k)}

.ENDS
*$

.SUBCKT BSS84P drain gate source PARAMS: Tj=23 Tcase=23 dVth=0 dRdson=0 dgfs=0 dC=0 dZth=0

.PARAM Rs=75.39m    Rg=10        Rd=30m       Rm=1m
.PARAM Inn=0.17     Unn=10       Rmax=8       gmin=71.59m
.PARAM RRf=500m     Rrbond=489   Rtb=1k       g2=1000m
.PARAM act=0.0915
.PARAM Ls=2n        Ld=1n        Lg=2n
.FUNC   Pb(I,dT,Rb)  {Rb/(2*Rtb)*(I-limit(dT/(max(I,1n)*Rb)+RRf*I*g2,0,I))**2}

X1  d1 g s Tj P_60_l_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
                                        +Rmax={Rmax} gmin={gmin} Rs={Rs} dC={dC} heat=1
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m))}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}
Rsb   source s1    10
Rga   gate   g1    10
Rdb   drain  d2    10

G_TH  0    Tb  VALUE =  {Pb(abs(I(Ls)),V(Tj,Tcase),Rrbond*(1+(limit((V(Tb)+V(Tj))/2,-200,999)-25)*4m))}
Cthb  Tb      0               944.25n
Rthb  Tb      Tj              {Rtb} 
Rth1  Tj      t1              {203.14m+limit(dZth,0,1)*75.18m}
Rth2  t1      t2              {2.8+limit(dZth,0,1)*1.04}
Rth3  t2      t3              {5.13+limit(dZth,0,1)*5.02}
Rth4  t3      t4              {8.9+limit(dZth,0,1)*19.37}
Rth5  t4      Tcase           {49.57+limit(dZth,0,1)*107.89}
Cth1  Tj      0               634n
Cth2  t1      0               782n
Cth3  t2      0               1.97u
Cth4  t3      0               30.6u
Cth5  t4      0               646u
Cth6  Tcase   0               1p

.ENDS

* MODELLING FOR STB16NF06LT4
 
.SUBCKT STB16NF06LT4 1 2 3
LG 2 4  7.5E-09
LS 12 3 7.5E-09
LD 6 1  4.5E-09
RG 4 5  4.995
RS 9 12 0.101E-02
RD 7 6  0.351E-01
RJ 8 7  0.101E-02
CGS 5 9   0.564E-09
CGD 7 10  0.743E-09
CK  11 7  0.308E-10
DGD 11 7 DGD
DBS 12 6 DBS
DBD  9 7 DBD
MOS  13 5 9 9 MOS L=1u W=1u
E1  10 5 101 0 1
E2  11 5 102 0 1
E3  8 13 POLY(2) 6 8 6 12 0 0 0 0  0.691
G1  0 100 7 5 1u
D1  100 101  DID
D2  102 100  DID
R1  101 0  1MEG
R2  102 0  1MEG
 
.MODEL MOS NMOS
+ LEVEL = 3
+ VTO   = 1.922
+ PHI   = 0.933
+ IS    = 0.1E-12
+ JS    = 0
+ THETA = 0.101E-01
+ KP    = 16.247
 
.MODEL DGD D
+ IS    = 0.1E-12
+ CJO   = 0.104E-10
+ VJ    = 0.718
+ M     = 0.357
.MODEL DBD D
+ IS    = 0.1E-12
+ CJO   = 0.232E-10
+ VJ    = 0.742
+ M     = 0.327
.MODEL DBS D
+ IS    = 0.1E-12
+ BV    = 69
+ N     = 1
+ TT    = 0.501E-07
+ RS    = 0.349E-02
.MODEL DID D
+ IS    = 0.01E-12
+ RS    = 0
+ BV    = 79
.ENDS STB16NF06LT4

* END OF MODELLING

*****************************************************************
* NXP 2N7002P
*
* Polarity N-Channel
* Ratings 60V/1.28E+00OHMS/0.5A
*
* Date Created Thu May 27 02:15:13 2010
*
*****************************************************************
* Model Generated by Transim Technology
*           Copyright(c) 2004
*         All Rights Reserved
*    UNPUBLISHED LICENSED SOFTWARE
*           www.transim.com
*
* Contains Proprietary Information Which
* is The Property of Transim or it's licensees.
*
* Commercial Use or Resale Restricted by
* Transim License Agreement.
*
*****************************************************************

.SUBCKT 2N7002P DRAIN GATE SOURCE

LD DRAIN 5 4e-10
RLD2 DRAIN 5 1.0053088
RLD1 5 4 0.0023
LG GATE 1 8.81711084834576e-10
RLG GATE 1 2.21597978160436
LS SOURCE 8 1.26727771949329e-09
RLS2 SOURCE 8 3.18501360862634
RLS1 8 7 0.0467180319746777

RDS 7 4 1200000000 TC=-0.05

RS 6 7 0.0001

RD 3 4 0.980671261025198 TC=0.00841797105604977,-1.47003132601411e-05
RBD 9 4 0.12 TC=0.005,2.19e-05
DBD 7 9 DBD

M1 3 2 6 6 MINT

RGS 2 6 1000000000000
CGS 2 6 3e-11
RG 1 2 0.947048638971317

* CGD
C11    11   12   1E-12
V11    11   0   0Vdc
G11    3 2 VALUE { V(13, 0)*I(V11) }
E11    12   0  3 2  1
E12    13   0  TABLE {V(12)}
+ -10	43
+ -5	40
+ -4	39
+ -3	33
+ -2	24
+ -1	21
+ -0.5	17
+ -0.2	15
+ -0.1	15
+ 0	13.85
+ 0.1	12.61
+ 0.2	12.15
+ 0.5	9.80
+ 1	7.82
+ 2	6.25
+ 5	4.70
+ 10	3.74
+ 12	3.52
+ 20	2.80
+ 25	2.58
+ 29.6	2.42



.MODEL MINT NMOS(Vto=1.975 Kp=1.0 Nfs=300000000000 Eta=1000
+ Level=3 L=1e-4 W=1e-4 Gamma=0 Phi=0.6 Is=1e-24
+ Js=0 Pb=0.8  Cj=0 Cjsw=0 Cgso=0 Cgdo=0 Cgbo=0
+ Tox=1e-07 Xj=0  
+ U0=600 Vmax=1000)

.MODEL DBD D(Bv=80.00 Ibv=1.00E-05 Rs=1E-6 Is=5e-15
+ N=1 M=0.5 VJ=0.5 Fc=0.5 Cjo=9.64e-12 Tt=3e-08)

.ENDS















